#+ method: GET
#+ summary: Query that given a standard variable, returns the calibrated model configurations which produce output variables with that standard name.
#+ defaults:
#+   - std: crop__simulated_produced_mass

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sdm: <https://w3id.org/okn/o/sdm#>
prefix sd: <https://w3id.org/okn/o/sd#>

SELECT DISTINCT ?model ?desc ?version ?configuration ?calibration ?compLoc ?category
from <http://ontosoft.isi.edu:3030/modelCatalog-1.2.0/data/mint@isi.edu>
WHERE {
  ?model a sdm:Model.
  ?model sd:hasVersion ?version.
  OPTIONAL{?model sdm:hasModelCategory ?category}.
  OPTIONAL{?model sd:description ?desc}.
  ?version sd:hasConfiguration ?configuration.
   ?configuration sd:hasSetup ?calibration
  OPTIONAL{?configuration sd:hasComponentLocation ?compLoc}.
  {
  ?calibration sd:hasOutput ?output.
  ?output sd:hasPresentation ?vp.
  ?vp sd:hasStandardVariable ?sv.
  ?sv rdfs:label ?_std_string.
}
UNION
{
    ?model sdm:usefulForCalculatingIndex ?sv.
    ?sv rdfs:label ?_std_string.
}
}