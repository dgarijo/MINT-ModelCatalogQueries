#+ method: GET
#+ summary: Given a model configuration URI, this query returns the parameters and their default values.
#+ defaults:
#+   - config: https://w3id.org/okn/i/mint/pihm-v2

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sd: <https://w3id.org/okn/o/sd#>
prefix sdm: <https://w3id.org/okn/o/sdm#>

SELECT  DISTINCT  ?p ?paramlabel ?description ?defaultvalue ?fixedValue ?pdatatype ?ptype ?minVal ?maxVal ?position ?var ?standardV ?unit ?intervention ?interventionDesc ?acceptedValues
from <http://ontosoft.isi.edu:3030/modelCatalog-1.1.0/data/mint@isi.edu> 
WHERE {
  ?_config_iri sd:hasParameter ?p.
  ?p rdfs:label ?paramlabel.
  ?p a ?ptype.
  FILTER (?ptype != sd:Parameter)
  OPTIONAL {?p sd:description ?description.}
  OPTIONAL {?p sd:hasDefaultValue ?defaultvalue.}
  OPTIONAL {?p sd:hasDataType ?pdatatype.}
  OPTIONAL {?p sd:hasMinimumAcceptedValue ?minVal.}
  OPTIONAL {?p sd:hasMaximumAcceptedValue ?maxVal.}
  OPTIONAL {?p sd:hasFixedValue ?fixedValue.}
  OPTIONAL {?p sd:position ?position.}
  OPTIONAL {?p sd:adjustsVariable ?var.?var sd:hasStandardVariable/rdfs:label ?standardV}
  OPTIONAL {?p sd:usesUnit ?u. ?u rdfs:label ?unit.}
  OPTIONAL {?p sdm:relevantForIntervention ?i.
            ?i rdfs:label ?intervention;
               sd:description ?interventionDesc.}
  OPTIONAL {?p sd:hasAcceptedValues ?acceptedValues.}
}